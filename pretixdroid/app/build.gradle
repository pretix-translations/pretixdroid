apply plugin: 'com.android.application'

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.3'

    defaultConfig {
        applicationId "eu.pretix.pretixdroid"
        minSdkVersion 16
        targetSdkVersion 27
        versionCode 15
        versionName "1.11.2"
        vectorDrawables.useSupportLibrary = true
    }

    lintOptions {
        disable 'InvalidPackage' // problem with jetty and bouncycastle
        disable 'MissingTranslation'
    }

    signingConfigs {
        release {
            storeFile file("../../release.keystore")
            storePassword System.getenv("KSTOREPWD")
            keyAlias "pretixdroid"
            keyPassword System.getenv("KSTOREPWD")
        }
    }

    buildTypes {
		def sentry_dsn = "";
		try {
			Properties properties = new Properties()
			properties.load(project.rootProject.file('local.properties').newDataInputStream())
			sentry_dsn = properties.getProperty('sentry.dsn')
			if (sentry_dsn == null) {
				sentry_dsn = "null"
			} else {
				sentry_dsn = "\"" + sentry_dsn + "\""
			}
		} catch (all) {
			sentry_dsn = "null"
		}

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            buildConfigField "String", "SENTRY_DSN", sentry_dsn
        }
        debug {
            debuggable true
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
            buildConfigField "String", "SENTRY_DSN", "null"
        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support:appcompat-v7:27.0.0'
    implementation 'com.android.support:design:27.0.0'
    implementation 'me.dm7.barcodescanner:zxing:1.9.3'
    implementation 'com.android.support:cardview-v7:27.0.0'
    implementation 'com.squareup.okhttp3:okhttp:3.9.0'
    implementation 'com.android.support:support-v4:27.0.0'
    implementation 'com.android.support:support-vector-drawable:27.0.0'
    implementation 'com.joshdholtz.sentry:sentry-android:1.6.0'
    implementation 'joda-time:joda-time:2.9.9'
    implementation 'io.requery:requery:1.4.1'
    implementation 'io.requery:requery-android:1.4.1'
    implementation 'com.facebook.stetho:stetho:1.5.0'
    implementation 'com.facebook.stetho:stetho-okhttp3:1.5.0'
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    annotationProcessor 'io.requery:requery-processor:1.4.1'
    implementation(project(':libpretixsync')) {
        transitive = false
    }
}
